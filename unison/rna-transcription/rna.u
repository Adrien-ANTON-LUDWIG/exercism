{{
  ``rna.toRNA`` is a function that takes a DNA strand and returns its RNA complement
  (per RNA transcription).
  If any of the characters in the DNA strand are not valid nucleotides, the function
  returns ``None``.

  **Examples**

  ```
  rna.toRNA "ACGTGGTCTTAA"
  ``` 

  ```
  rna.toRNA "ACGTXXXCTTAA"
  ```
}}
rna.toRNA : Text -> Optional Text
rna.toRNA dna =
  dna
    |> toCharList
    |> map rna.mapNucleotide
    |> foldLeft (map2 (Text.++)) (Some "")

{{
  ``rna.mapNucleotide`` is a function that takes a DNA nucleotide and returns its
  RNA complement (per RNA transcription).
  If the character is not a valid nucleotide, the function returns ``None``.

  **Examples**

  ```
  rna.mapNucleotide ?G
  ``` 

  ```
  rna.mapNucleotide ?X
  ``` 
}}
rna.mapNucleotide : Char -> Optional Text
rna.mapNucleotide = cases
  ?G -> Some "C"
  ?C -> Some "G"
  ?T -> Some "A"
  ?A -> Some "U"

  -- If the character is not a valid nucleotide, return ``None``
  _ -> None
